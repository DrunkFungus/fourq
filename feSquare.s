// func feSquare(c, a *fieldElem)
TEXT Â·feSquare(SB),0,$0-16
	MOVQ a+8(FP), DI

	// Compute a.x + a.y = 0(DI)||8(DI) + 16(DI)||24(DI). Store in (R11*2^64+R10).
	MOVQ 0(DI), R10
	MOVQ 8(DI), R11
	ADDQ 16(DI), R10
	ADCQ 24(DI), R11
	BTRQ $63, R11
	ADCQ $0, R10
	ADCQ $0, R11

	// Compute a.x - a.y = 0(DI)||8(DI) - 16(DI)||24(DI). Store in (R13*2^64+R12).
	MOVQ 16(DI), R12
	MOVQ 24(DI), R13

	NOTQ R12
	NOTQ R13
	BTRQ $63, R13
	ADDQ 0(DI), R12
	ADCQ 8(DI), R13
	BTRQ $63, R13
	ADCQ $0, R12
	ADCQ $0, R13

	// Mult (R11*2^64+R10) * (R13*2^64+R12). Store in (R9*2^64+R8).
	MOVQ $0, CX
	MOVQ R10, AX
	MULQ R12
	MOVQ AX, R8
	MOVQ DX, R9
	MOVQ R10, AX
	MULQ R13
	SHLQ $1, DX
	ADDQ DX, R8
	ADCQ AX, R9
	ADCQ $0, CX
	MOVQ R11, AX
	MULQ R12
	SHLQ $1, DX
	ADDQ DX, R8
	ADCQ AX, R9
	ADCQ $0, CX
	MOVQ R11, AX
	MULQ R13
	SHLQ $1, DX
	SHLQ $1, AX
	ADCQ $0, DX
	ADDQ AX, R8
	ADCQ DX, R9
	ADCQ $0, CX

	SHLQ $1, CX
	BTRQ $63, R9
	ADCQ CX, R8
	ADCQ $0, R9
	BTRQ $63, R9
	ADCQ $0, R8
	ADCQ $0, R9

	// Mult a.x * a.y = 0(DI)||8(DI) * 16(DI)||24(DI). Store in (R11*2^64+R10).
	MOVQ $0, CX
	MOVQ 0(DI), AX
	MULQ 16(DI)
	MOVQ AX, R10
	MOVQ DX, R11
	MOVQ 0(DI), AX
	MULQ 24(DI)
	SHLQ $1, DX
	ADDQ DX, R10
	ADCQ AX, R11
	ADCQ $0, CX
	MOVQ 8(DI), AX
	MULQ 16(DI)
	SHLQ $1, DX
	ADDQ DX, R10
	ADCQ AX, R11
	ADCQ $0, CX
	MOVQ 8(DI), AX
	MULQ 24(DI)
	SHLQ $1, DX
	SHLQ $1, AX
	ADCQ $0, DX
	ADDQ AX, R10
	ADCQ DX, R11
	ADCQ $0, CX

	SHLQ $1, CX
	BTRQ $63, R11
	ADCQ CX, R10
	ADCQ $0, R11
	BTRQ $63, R11
	ADCQ $0, R10
	ADCQ $0, R11

	// Double (R11*2^64+R10) in-place.
	SHLQ $1, R11
	SHLQ $1, R10
	ADCQ $0, R11
	BTRQ $63, R11
	ADCQ $0, R10
	ADCQ $0, R11

	// Move out.
	MOVQ c+0(FP), DI
	MOVQ R8, 0(DI)
	MOVQ R9, 8(DI)
	MOVQ R10, 16(DI)
	MOVQ R11, 24(DI)
	RET
